buildscript {
	apply from: file('gradle/buildscript.gradle'), to: buildscript
}

apply plugin: 'org.anarres.stdproject'
stdproject {
}

subprojects {
	group = "org.anarres.dblx"

	apply plugin: 'org.anarres.stdmodule'
	stdmodule {
		description "Dr Brainlove Lighting Control"
		author id: 'shevek', name: 'Shevek', email: 'github@anarres.org'
		license 'Apache-2.0'
	}

	repositories {
		mavenLocal()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
	}

	dependencies {
		compile 'com.google.code.findbugs:annotations:2.0.3'
		compile 'org.slf4j:slf4j-api:1.7.12'
	}

	sourceCompatibility = 1.7
	animalsniffer {
		signature = "org.codehaus.mojo.signature:java17:+@signature"
		skip = true
	}
}

def springVersion = '4.2.6.RELEASE'

project(':dblx-core') {
	dependencies {
		compile 'com.google.guava:guava:18.0'
		compile 'org.jgrapht:jgrapht-core:0.9.1'
		compile 'org.jscience:jscience:4.3.1'

		compile 'javax.vecmath:vecmath:1.5.2'
		// compile 'org.scijava:vecmath:1.6.0-scijava-2'

		// compile 'org.processing:processing-complete:3.0.1'
		compile 'org.processing:core:3.0.1'
		// compile 'org.processing:net:3.0.1'
		// compile 'org.processing:pde:3.0.1'
		// compile 'org.processing:serial:3.0.1'

		compile 'org.anarres.mirrors.heronarts:p3lx:1.0.0-SNAPSHOT'

		compile 'org.biojava:jcolorbrewer:5.2'
		compile 'com.opencsv:opencsv:3.7'

		// Assembly
		compile 'com.google.auto.service:auto-service:1.0-rc2'
		compile "org.springframework:spring-context-support:$springVersion"
		compile 'net.sf.jopt-simple:jopt-simple:4.8'
		compile 'org.anarres.jdiagnostics:jdiagnostics:1.0.2'

		testCompile 'org.choco-solver:choco-graph:3.3.0'
	}
}

project(':dblx-ui') {
	dependencies {
		compile project(':dblx-core')
	}
}

project(':dblx-iface-muse') {
	dependencies {
		compile project(':dblx-core')
		compile 'org.anarres.mirrors.processing:oscp5:2.0.0-SNAPSHOT'
	}
}

project(':dblx-iface-evolution') {
	dependencies {
		compile project(':dblx-core')
	}
}

project(':dblx-output-beaglebone') {
	dependencies {
		compile project(':dblx-core')
	}
}

project(':dblx-package') {
	dependencies {
		compile project(':dblx-core')
		compile project(':dblx-ui')
		compile project(':dblx-iface-muse')
		compile project(':dblx-iface-evolution')
		compile project(':dblx-output-beaglebone')
	}

	apply plugin: 'application'
	mainClassName = 'org.anarres.dblx.core.Main'

	apply plugin: 'nebula.ospackage'

	ospackage {
		packageName = "dblx"
		version = project.version + new Date().format(".yyyyMMdd")

		packageDescription = "Dr. Brainlove LED Lighting Controller."
		packager = "Shevek <shevek@anarres.org>"
		vendor = packager
		url = "https://github.com/shevek/dblx/"
		os = LINUX

		into "/opt/dblx"

		from("$project.buildDir/install/dblx-package/lib") {
			into "lib"
			fileMode 0644
		}
		from("$project.buildDir/install/dblx-package/bin") {
			into "bin"
			fileMode 0755
		}
	}

	buildDeb {
		dependsOn installApp
	}

	buildRpm {
		dependsOn installApp
	}
}
